apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

def description = "Provides Kotlin Coroutines suspendable await() extensions for Retrofit Call"
def githubId = "gildor/kotlin-coroutines-retrofit"
def repoWeb = "https://github.com/${githubId}"
def repoVcs = "${repoWeb}.git"
def issueTracker = "${repoWeb}/issues"
def tags = ['retrofit', 'kotlin', 'coroutines']
def licenseId = "Apache-2.0"
def licenseName = "The Apache Software License, Version 2.0"
def licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
def releaseTag = "v${project.version}"

// Create the pom configuration:
def pomConfig = {
    developers {
        developer {
            name "Andrey Mischenko"
            email "git@gildor.ru"
            organizationUrl "https://github.com/gildor"
        }
    }
    scm {
        url repoWeb
        connection "scm:git:${repoVcs}"
        developerConnection "scm:git:${repoVcs}"
        tag releaseTag
    }
    issueManagement {
        system "GitHub Issues"
        url issueTracker
    }
    licenses {
        license {
            name licenseName
            url licenseUrl
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir

    externalDocumentationLink {
        url = new URL("https://square.github.io/retrofit/2.x/retrofit/")
        packageListUrl = new URL("https://square.github.io/retrofit/2.x/retrofit/package-list")
    }
    externalDocumentationLink {
        url = new URL("https://square.github.io/okhttp/3.x/okhttp/")
        packageListUrl = new URL("https://square.github.io/okhttp/3.x/okhttp/package-list")
    }

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/gildor/kotlin-coroutines-retrofit/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', description)
                root.appendNode('url', repoWeb)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.properties['bintray.user']
    key = project.properties['bintray.key']
    publications = ['Publication']
    publish = true
    pkg {
        repo = project.properties['bintray.repo'] ?: 'maven'
        name = project.name
        desc = description
        githubRepo = githubId
        githubReleaseNotesFile = 'CHANGELOG.md'
        websiteUrl = repoWeb
        issueTrackerUrl = "${repoWeb}/issues"
        vcsUrl = repoVcs
        licenses = [licenseId]
        labels = tags
        version {
            name = project.version
            vcsTag = releaseTag
            mavenCentralSync {
                sync = project.properties['sonatype.user'] != null
                user = project.properties['sonatype.user']
                password = project.properties['sonatype.password']
                close = true
            }
        }
    }
}
